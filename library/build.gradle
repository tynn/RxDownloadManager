apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

android {
    defaultConfig {
        versionCode 11
    }
}

dependencies {
    api 'io.reactivex:rxjava:1.3.8'

    implementation 'io.reactivex:rxandroid:1.2.1'

    compileOnly 'androidx.annotation:annotation:1.1.0'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.28.2'
    testImplementation 'org.powermock:powermock-api-mockito2:2.0.2'
    testImplementation 'org.powermock:powermock-module-junit4:2.0.2'
    testImplementation configurations.compileOnly
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options {
        links "https://d.android.com/reference/"
        linksOffline "https://d.android.com/reference/", "${android.sdkDirectory}/docs/reference/"
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
