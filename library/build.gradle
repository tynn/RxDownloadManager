apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

android {
    compileSdkVersion xTargetSdkVersion
    buildToolsVersion xBuildToolsVersion

    defaultConfig {
        versionCode 11
        minSdkVersion 15
        targetSdkVersion xTargetSdkVersion
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    compile "io.reactivex:rxjava:$rxJavaVersion"
    compile "io.reactivex:rxandroid:$rxAndroidVersion"
    provided "com.android.support:support-annotations:$supportVersion"
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.powermock:powermock-api-mockito:1.6.6'
    testCompile 'org.powermock:powermock-module-junit4:1.6.6'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
    classpath += configurations.provided
    options {
        links "https://d.android.com/reference/"
        linksOffline "https://d.android.com/reference/", "${android.sdkDirectory}/docs/reference/"
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
